<html>
<head><meta charset="utf-8"><title>[isabelle] Some lemmas on factorial rings · Mirror: Isabelle Users Mailing List · Zulip Chat Archive</title></head>
<h2>Stream: <a href="http://isabelle.systems/zulip-archive/stream/247541-Mirror:-Isabelle-Users-Mailing-List/index.html">Mirror: Isabelle Users Mailing List</a></h2>
<h3>Topic: <a href="http://isabelle.systems/zulip-archive/stream/247541-Mirror:-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20Some.20lemmas.20on.20factorial.20rings.html">[isabelle] Some lemmas on factorial rings</a></h3>

<hr>

<base href="https://isabelle.zulipchat.com/">

<head><link href="http://isabelle.systems/zulip-archive/style.css" rel="stylesheet"></head>

<a name="222661404"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/247541-Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20Some%20lemmas%20on%20factorial%20rings/near/222661404" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/247541-Mirror:-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20Some.20lemmas.20on.20factorial.20rings.html#222661404">(Jan 13 2021 at 23:21)</a>:</h4>
<p>From: Jakub Kądziołka &lt;<a href="mailto:kuba@kadziolka.net">kuba@kadziolka.net</a>&gt;<br>
Hello,</p>
<p>I have proven a number of lemmas today that, I believe, should be<br>
included in the distribution.</p>
<p>Regards,<br>
Jakub Kądziołka</p>
<p>P.S. My previous suggestion regarding a HOL-Algebra lemma [1] went without a<br>
response. I'd appreciate someone looking into it.</p>
<p>[1]: <a href="https://lists.cam.ac.uk/mailman/htdig/cl-isabelle-users/2020-November/msg00043.html">https://lists.cam.ac.uk/mailman/htdig/cl-isabelle-users/2020-November/msg00043.html</a></p>
<p>context factorial_semiring<br>
begin</p>
<p>lemma infinite_unit_divisor_powers:<br>
  assumes "y ≠ 0"<br>
  assumes "is_unit x"<br>
  shows "infinite {n. x^n dvd y}"<br>
proof -<br>
  from <code>is_unit x</code> have "is_unit (x^n)" for n<br>
    using is_unit_power_iff by auto<br>
  hence "x^n dvd y" for n<br>
    by auto<br>
  hence "{n. x^n dvd y} = UNIV"<br>
    by auto<br>
  thus ?thesis<br>
    by auto<br>
qed</p>
<p>corollary is_unit_iff_infinite_divisor_powers:<br>
  assumes "y ≠ 0"<br>
  shows "is_unit x ⟷ infinite {n. x^n dvd y}"<br>
  using infinite_unit_divisor_powers finite_divisor_powers assms by auto</p>
<p>lemma multiplicity_zero_left [simp]: "multiplicity 0 x = 0"<br>
  apply (cases "x = 0")<br>
  by (auto intro: not_dvd_imp_multiplicity_0)</p>
<p>lemma multiplicity_dvd_iff_dvd:<br>
  assumes "x ≠ 0"<br>
  shows "p^k dvd x ⟷ p^k dvd p^multiplicity p x"<br>
proof (cases "is_unit p")<br>
  case True<br>
  then have "is_unit (p^k)"<br>
    using is_unit_power_iff by simp<br>
  hence "p^k dvd x"<br>
    by auto<br>
  moreover from <code>is_unit p</code> have "p^k dvd p^multiplicity p x"<br>
    using multiplicity_unit_left is_unit_power_iff by simp<br>
  ultimately show ?thesis by simp<br>
next<br>
  case False<br>
  show ?thesis<br>
  proof (cases "p = 0")<br>
    case True<br>
    then have "p^multiplicity p x = 1"<br>
      by simp<br>
    moreover have "p^k dvd x ⟹ k = 0"<br>
    proof (rule ccontr)<br>
      assume "p^k dvd x" and "k ≠ 0"<br>
      with <code>p = 0</code> have "p^k = 0" by auto<br>
      with <code>p^k dvd x</code> have "0 dvd x" by auto<br>
      hence "x = 0" by auto<br>
      with <code>x ≠ 0</code> show False by auto<br>
    qed<br>
    ultimately show ?thesis<br>
      by (auto simp add: is_unit_power_iff <code>¬ is_unit p</code>)<br>
  next<br>
    case False<br>
    with <code>x ≠ 0</code> <code>¬ is_unit p</code> show ?thesis<br>
      by (simp add: power_dvd_iff_le_multiplicity dvd_power_iff multiplicity_same_power)<br>
  qed<br>
qed</p>
<p>lemma multiplicity_decomposeI:<br>
  assumes "x = p^k * x'" and "¬ p dvd x'" and "p ≠ 0"<br>
  shows "multiplicity p x = k"<br>
proof (rule multiplicity_eqI)<br>
  from assms show "p^k dvd x" by auto<br>
  from assms have "x = x' * p^k" by (simp add: ac_simps)<br>
  with <code>¬ p dvd x'</code> and <code>p ≠ 0</code> show "¬ p^Suc k dvd x"<br>
    by simp<br>
qed</p>
<p>lemma multiplicity_sum_lt:<br>
  assumes "multiplicity p a &lt; multiplicity p b" "a ≠ 0" "b ≠ 0"<br>
  shows "multiplicity p (a + b) = multiplicity p a"<br>
proof -<br>
  let ?vp = "multiplicity p"<br>
  have unit: "¬ is_unit p"<br>
  proof<br>
    assume "is_unit p"<br>
    then have "?vp a = 0" and "?vp b = 0" using multiplicity_unit_left by auto<br>
    with assms show False by auto<br>
  qed</p>
<p>from multiplicity_decompose' obtain a' where a': "a = p^?vp a * a'" "¬ p dvd a'"<br>
    using unit assms by metis<br>
  from multiplicity_decompose' obtain b' where b': "b = p^?vp b * b'"<br>
    using unit assms by metis</p>
<p>show "?vp (a + b) = ?vp a"<br>
    ― ‹Use the rule here, after we obtained @{term a'} and @{term b'}, to avoid the<br>
          "Result contains obtained parameters" error›<br>
  proof (rule multiplicity_decomposeI)<br>
    let ?k = "?vp b - ?vp a"<br>
    from assms have k: "?k &gt; 0" by simp<br>
    with b' have "b = p^?vp a * p^?k * b'"<br>
      by (simp flip: power_add)<br>
    with a' show *: "a + b = p^?vp a * (a' + p^?k * b')"<br>
      by (simp add: ac_simps distrib_left)</p>
<p>moreover show "¬ p dvd a' + p^?k * b'"<br>
      using a' k dvd_add_left_iff by auto</p>
<p>show "p ≠ 0" using assms by auto<br>
  qed<br>
qed</p>
<p>corollary multiplicity_sum_min:<br>
  assumes "multiplicity p a ≠ multiplicity p b" "a ≠ 0" "b ≠ 0"<br>
  shows "multiplicity p (a + b) = min (multiplicity p a) (multiplicity p b)"<br>
proof -<br>
  let ?vp = "multiplicity p"<br>
  from assms have "?vp a &lt; ?vp b ∨ ?vp a &gt; ?vp b"<br>
    by auto<br>
  then show ?thesis<br>
    by (metis assms multiplicity_sum_lt min.commute add_commute min.strict_order_iff)    <br>
qed</p>
<p>end</p>



<a name="222732641"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/247541-Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20Some%20lemmas%20on%20factorial%20rings/near/222732641" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/247541-Mirror:-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20Some.20lemmas.20on.20factorial.20rings.html#222732641">(Jan 14 2021 at 15:28)</a>:</h4>
<p>From: Florian Haftmann &lt;<a href="mailto:florian.haftmann@informatik.tu-muenchen.de">florian.haftmann@informatik.tu-muenchen.de</a>&gt;<br>
Hi Jakub,</p>
<p>thanks for contributing this.</p>
<p>I will put it into the distribution after the upcoming Isabelle2021 release.</p>
<p>Cheers,<br>
    Florian<br>
<a href="/user_uploads/14278/DXCcxHbQnE_-DgUFb7sLaA9I/signature.asc">signature.asc</a></p>



<hr><p>Last updated: Jul 15 2022 at 23:21 UTC</p>
</html>