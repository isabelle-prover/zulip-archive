<html>
<head><meta charset="utf-8"><title>[isabelle] Weak lemma:	HOL-Algebra.Multiplicative_Group.g... · Mirror: Isabelle Users Mailing List · Zulip Chat Archive</title></head>
<h2>Stream: <a href="http://isabelle.systems/zulip-archive/stream/247541-Mirror:-Isabelle-Users-Mailing-List/index.html">Mirror: Isabelle Users Mailing List</a></h2>
<h3>Topic: <a href="http://isabelle.systems/zulip-archive/stream/247541-Mirror:-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20Weak.20lemma.3A.09HOL-Algebra.2EMultiplicative_Group.2Eg.2E.2E.2E.html">[isabelle] Weak lemma:	HOL-Algebra.Multiplicative_Group.g...</a></h3>

<hr>

<base href="https://isabelle.zulipchat.com/">

<head><link href="http://isabelle.systems/zulip-archive/style.css" rel="stylesheet"></head>

<a name="216652420"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/247541-Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20Weak%20lemma%3A%09HOL-Algebra.Multiplicative_Group.g.../near/216652420" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/247541-Mirror:-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20Weak.20lemma.3A.09HOL-Algebra.2EMultiplicative_Group.2Eg.2E.2E.2E.html#216652420">(Nov 13 2020 at 18:30)</a>:</h4>
<p>From: Jakub Kądziołka &lt;<a href="mailto:kuba@kadziolka.net">kuba@kadziolka.net</a>&gt;<br>
Greetings,</p>
<p>currently, generate_pow_card assumes "finite (carrier G)". This is<br>
unnecessary, as evidenced by the modified proof I'm including below.<br>
I modified two lemmas above it to only assume that a relevant subgroup<br>
is finite, instead of the entire group. Also, the removed assumption<br>
leads to a simpler proof of ord_dvd_group_order.</p>
<p>Regards,<br>
Jakub Kądziołka</p>
<p>theory Scratch<br>
  imports "HOL-Algebra.Algebra"<br>
begin</p>
<p>context group begin</p>
<p>lemma ord_elems_inf_carrier:<br>
  assumes "a ∈ carrier G" "ord a ≠ 0"<br>
  shows "{a[^]x | x. x ∈ (UNIV :: nat set)} = {a[^]x | x. x ∈ {0 .. ord a - 1}}" (is "?L = ?R")<br>
proof<br>
  show "?R ⊆ ?L" by blast<br>
  { fix y assume "y ∈ ?L"<br>
    then obtain x::nat where x: "y = a[^]x" by auto<br>
    define r q where "r = x mod ord a" and "q = x div ord a"<br>
    then have "x = q * ord a + r"<br>
      by (simp add: div_mult_mod_eq)<br>
    hence "y = (a[^]ord a)[^]q ⊗ a[^]r"<br>
      using x assms by (metis mult.commute nat_pow_mult nat_pow_pow)<br>
    hence "y = a[^]r" using assms by simp<br>
    have "r &lt; ord a" using assms by (simp add: r_def)<br>
    hence "r ∈ {0 .. ord a - 1}" by (force simp: r_def)<br>
    hence "y ∈ {a[^]x | x. x ∈ {0 .. ord a - 1}}" using ‹y=a[^]r› by blast<br>
  }<br>
  thus "?L ⊆ ?R" by auto<br>
qed</p>
<p>lemma generate_pow_nat:<br>
  assumes a: "a ∈ carrier G" and "ord a ≠ 0"<br>
  shows "generate G { a } = { a [^] k | k. k ∈ (UNIV :: nat set) }"<br>
proof<br>
  show "{ a [^] k | k. k ∈ (UNIV :: nat set) } ⊆ generate G { a }"<br>
  proof<br>
    fix b assume "b ∈ { a [^] k | k. k ∈ (UNIV :: nat set) }"<br>
    then obtain k :: nat where "b = a [^] k" by blast<br>
    hence "b = a [^] (int k)"<br>
      by (simp add: int_pow_int)<br>
    thus "b ∈ generate G { a }"<br>
      unfolding generate_pow[OF a] by blast<br>
  qed<br>
next<br>
  show "generate G { a } ⊆ { a [^] k | k. k ∈ (UNIV :: nat set) }"<br>
  proof<br>
    fix b assume "b ∈ generate G { a }"<br>
    then obtain k :: int where k: "b = a [^] k"<br>
      unfolding generate_pow[OF a] by blast<br>
    show "b ∈ { a [^] k | k. k ∈ (UNIV :: nat set) }"<br>
    proof (cases "k &lt; 0")<br>
      assume "¬ k &lt; 0"<br>
      hence "b = a [^] (nat k)"<br>
        by (simp add: k)<br>
      thus ?thesis by blast<br>
    next<br>
      assume "k &lt; 0"<br>
      hence b: "b = inv (a [^] (nat (- k)))"<br>
        using k a by (auto simp: int_pow_neg)<br>
      obtain m where m: "ord a * m ≥ nat (- k)"<br>
        by (metis assms(2) dvd_imp_le dvd_triv_right le_zero_eq mult_eq_0_iff not_gr_zero)<br>
      hence "a [^] (ord a * m) = 𝟭"<br>
        by (metis a nat_pow_one nat_pow_pow pow_ord_eq_1)<br>
      then obtain k' :: nat where "(a [^] (nat (- k))) ⊗ (a [^] k') = 𝟭"<br>
        using m a nat_le_iff_add nat_pow_mult by auto<br>
      hence "b = a [^] k'"<br>
        using b a by (metis inv_unique' nat_pow_closed nat_pow_comm)<br>
      thus "b ∈ { a [^] k | k. k ∈ (UNIV :: nat set) }" by blast<br>
    qed<br>
  qed<br>
qed</p>
<p>lemma generate_pow_card:<br>
  assumes a: "a ∈ carrier G"<br>
  shows "ord a = card (generate G { a })"<br>
proof (cases "ord a = 0")<br>
  case True<br>
  then have "infinite (carrier (subgroup_generated G {a}))"<br>
    using infinite_cyclic_subgroup_order[OF a] by auto<br>
  then have "infinite (generate G {a})"<br>
    unfolding subgroup_generated_def<br>
    using a by simp<br>
  then show ?thesis<br>
    using <code>ord a = 0</code> by auto<br>
next<br>
  case False<br>
  note finite_subgroup = this<br>
  then have "generate G { a } = (([^]) a) ` {0..ord a - 1}"<br>
    using generate_pow_nat ord_elems_inf_carrier a by auto<br>
  hence "card (generate G {a}) = card {0..ord a - 1}"<br>
    using ord_inj[OF a] card_image by metis<br>
  also have "... = ord a" using finite_subgroup by auto<br>
  finally show ?thesis.. <br>
qed</p>
<p>lemma ord_dvd_group_order: <br>
  assumes "a ∈ carrier G"<br>
  shows "(ord a) dvd (order G)"<br>
proof -<br>
  show ?thesis<br>
    using lagrange[OF generate_is_subgroup[of "{a}"]] assms<br>
    unfolding generate_pow_card[OF assms]<br>
    by (metis dvd_triv_right empty_subsetI insert_subset)<br>
qed</p>
<p>end<br>
end</p>



<hr><p>Last updated: Jul 15 2022 at 23:21 UTC</p>
</html>