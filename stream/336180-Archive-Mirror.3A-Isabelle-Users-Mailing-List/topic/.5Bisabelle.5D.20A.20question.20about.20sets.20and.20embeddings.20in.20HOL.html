<html>
<head><meta charset="utf-8"><title>[isabelle] A question about sets and embeddings in HOL · Archive Mirror: Isabelle Users Mailing List · Zulip Chat Archive</title></head>
<h2>Stream: <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/index.html">Archive Mirror: Isabelle Users Mailing List</a></h2>
<h3>Topic: <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html">[isabelle] A question about sets and embeddings in HOL</a></h3>

<hr>

<base href="https://isabelle.zulipchat.com/">

<head><link href="http://isabelle.systems/zulip-archive/style.css" rel="stylesheet"></head>

<a name="294671805"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294671805" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294671805">(Aug 22 2022 at 13:06)</a>:</h4>
<p>From: "Eugene W. Stark" &lt;<a href="mailto:isabelle-users@starkeffect.com">isabelle-users@starkeffect.com</a>&gt;<br>
Is it possible, in HOL, to prove that for any I-indexed collection of sets<br>
{F i: i ∈ I} there exists a set S that embeds (via injective maps) all of<br>
the sets F i and furthermore is weakly universal for this property, so that<br>
if S' is any other set that embeds all the F i then S' embeds S?<br>
In more detail, I am thinking of something like the following:</p>
<p>definition embeds<br>
  where "embeds A B ≡ ∃f. f ∈ B → A ∧ inj_on f B"</p>
<p>lemma "∀F. ∃S. (∀x. embeds S (F x)) ∧ (∀S'. (∀x. embeds S' (F x)) ⟶ embeds S' S)"</p>
<p>In ZFC I suppose it would just be possible to take as S the least cardinal<br>
greater than the cardinals of all the F i, but I don't have a very clear<br>
idea of how/whether something similar could be done in HOL.</p>
<p>Before I spend a lot of time rummaging through the well ordering stuff in<br>
the Isabelle library I thought I would risk asking to see if somebody on<br>
this list knows the answer instantly.  Thanks for any help you can give.</p>
<p>- Gene Stark</p>



<a name="294671825"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294671825" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294671825">(Aug 22 2022 at 13:07)</a>:</h4>
<p>From: Andrei Popescu &lt;<a href="mailto:A.Popescu@mdx.ac.uk">A.Popescu@mdx.ac.uk</a>&gt;<br>
Hi Gene, </p>
<p>You can use the disjoint union (which is of course not only weakly, but also strongly universal). <br>
Search for the text "Disjoint union of a family of sets" in <a href="https://isabelle.in.tum.de/dist/library/HOL/HOL/Product_Type.html">https://isabelle.in.tum.de/dist/library/HOL/HOL/Product_Type.html</a></p>
<p>All the best, <br>
  Andrei</p>



<a name="294671863"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294671863" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294671863">(Aug 22 2022 at 13:07)</a>:</h4>
<p>From: Dmitriy Traytel &lt;<a href="mailto:traytel@inf.ethz.ch">traytel@inf.ethz.ch</a>&gt;<br>
Hi Gene,</p>
<p>while Andrei is right on the high level, I should point out that your lemma does not hold in HOL as stated.</p>
<p>For instance the type of the existentially quantified "S :: ‘c set" might simply be not large enough to embed "(F :: ‘a =&gt; ‘b set) i” (counterexample: ‘c = unit, ‘a = ‘b = nat, F = %_. UNIV).</p>
<p>Here is a theorem, which I can prove easily using our cardinals library [1]. It fixes ‘c to be a large enough type to use Sigma as the witness (as Andrei suggested). It also requires the index type ‘a to be infinite and S’ to be larger than the index type as well.</p>
<p>theory Scratch<br>
imports<br>
  "~~/src/HOL/Library/Cardinal_Notations"<br>
  "~~/src/HOL/Library/FuncSet"<br>
begin</p>
<p>definition embeds<br>
 where "embeds A B ≡ ∃f. f ∈ B → A ∧ inj_on f B"</p>
<p>lemma embeds_ordLeq: "embeds A B ⟷ |B| ≤o |A|"<br>
  unfolding card_of_ordLeq[symmetric] embeds_def by auto</p>
<p>lemma<br>
  fixes F :: "'a ⇒ 'b set"<br>
  assumes "infinite (UNIV :: 'a set)"<br>
  shows "∃S :: ('a × 'b) set.<br>
    (∀x. embeds S (F x)) ∧<br>
    (∀S'. (∀x. embeds S' (F x)) ⟶ embeds S' (UNIV :: 'a set) ⟶ embeds S' S)"<br>
  unfolding embeds_ordLeq using assms<br>
  by (auto simp: image_iff card_of_ordLeq_finite<br>
   intro!: exI[of _ "Sigma UNIV F"] surj_imp_ordLeq[of _ snd] card_of_Sigma_ordLeq_infinite)</p>
<p>end</p>
<p>I did not think thoroughly if all my modifications are necessary. I.e., what happens if the index type ‘a is finite?</p>
<p>Best wishes,<br>
Dmitriy</p>
<p>[1] Cardinals in Isabelle/HOL<br>
Jasmin Christian Blanchette, Andrei Popescu, Dmitriy Traytel<br>
ITP 2014, <a href="http://people.inf.ethz.ch/trayteld/papers/itp14-card/card.pdf">http://people.inf.ethz.ch/trayteld/papers/itp14-card/card.pdf</a></p>



<a name="294671897"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294671897" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294671897">(Aug 22 2022 at 13:07)</a>:</h4>
<p>From: "Eugene W. Stark" &lt;<a href="mailto:isabelle-users@starkeffect.com">isabelle-users@starkeffect.com</a>&gt;<br>
Andrei, the disjoint union was not what I wanted, because although it<br>
does in fact embed all the sets, it separates them and thus produces a set that<br>
is not minimal with respect to embeddings.  If we make identifications we<br>
can get a more economical union.  For example if A = {*}, then the disjoint<br>
union A+A of A and A is a two-element set, but A itself already embeds A<br>
and has only one element, so is itself embedded in A+A.</p>
<p>- Gene Stark</p>



<a name="294671919"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294671919" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294671919">(Aug 22 2022 at 13:07)</a>:</h4>
<p>From: Andrei Popescu &lt;<a href="mailto:a.popescu@mdx.ac.uk">a.popescu@mdx.ac.uk</a>&gt;<br>
Hi Dmitriy and Gene, </p>
<p>Sorry, I lost sight of the word "embedding" in Gene's email... </p>
<p>Indeed, disjoint union does not fully work, as shown by Dmitriy's need for extra hypotheses. In particular, the size of the index causes trouble. <br>
For a full solution, we really need cardinal suprema. In HOL, this is a bit more bureaucratic than in ZFC, but it works. <br>
We can fix a well-order on 'b (where the family F has type 'a =&gt; 'b set). <br>
For each i::'a, there exists a restriction of this fixed well-order, say, "k i", representing the cardinal of "F i".<br>
The desired weakly universal set is the union of the fields of all these cardinals "k i".  </p>
<p>All the best, <br>
   Andrei</p>



<a name="294671952"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294671952" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294671952">(Aug 22 2022 at 13:07)</a>:</h4>
<p>From: Andrei Popescu &lt;<a href="mailto:a.popescu@mdx.ac.uk">a.popescu@mdx.ac.uk</a>&gt;<br>
Hi Gene, </p>
<p>Correct. In my other email I sketch such a (most) economical union, namely, on cardinal representatives. <br>
This is useful result in our cardinal library, so I can add it myself. </p>
<p>Best, <br>
  Andrei</p>



<a name="294671971"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294671971" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294671971">(Aug 22 2022 at 13:07)</a>:</h4>
<p>From: "Eugene W. Stark" &lt;<a href="mailto:isabelle-users@starkeffect.com">isabelle-users@starkeffect.com</a>&gt;<br>
That sounds like the answer I was looking for.  Thank you both for your replies.</p>
<p>- Gene Stark</p>



<a name="294672079"></a>
<h4><a href="https://isabelle.zulipchat.com/#narrow/stream/336180-Archive%20Mirror%3A%20Isabelle%20Users%20Mailing%20List/topic/%5Bisabelle%5D%20A%20question%20about%20sets%20and%20embeddings%20in%20HOL/near/294672079" class="zl"><img src="http://isabelle.systems/zulip-archive/assets/img/zulip.svg" alt="view this post on Zulip" style="width:20px;height:20px;"></a> Email Gateway <a href="http://isabelle.systems/zulip-archive/stream/336180-Archive-Mirror.3A-Isabelle-Users-Mailing-List/topic/.5Bisabelle.5D.20A.20question.20about.20sets.20and.20embeddings.20in.20HOL.html#294672079">(Aug 22 2022 at 13:08)</a>:</h4>
<p>From: Andrei Popescu &lt;<a href="mailto:A.Popescu@mdx.ac.uk">A.Popescu@mdx.ac.uk</a>&gt;<br>
Hi Gene,</p>
<p>Thank you for your question -- this theorem was an important fact missing in our cardinal library, <br>
and it triggered some further useful infrastructure lemmas. </p>
<p>A polished version of it will be part of the next distribution. In the meantime, attached is a theory file <br>
that has the theorem at the end (also included in this message).  </p>
<p>All the best, <br>
  Andrei </p>
<p>theory Embed imports Main<br>
begin</p>
<p>(* Some order-filter infrastructure *)</p>
<p>lemma ofilter_embed2:<br>
assumes r: "Well_order r" and e: "embed r r' f" and o: "ofilter r A"<br>
shows "embed (Restr r A) r' f"<br>
using assms Field_Restr_ofilter[OF r o] ofilter_Restr_under[OF r o] <br>
unfolding embed_def ofilter_embed[OF r] by auto</p>
<p>lemma ofilter_incl_embed: <br>
assumes r: "Well_order r" and oA: "ofilter r A" and oB: "ofilter r B"<br>
and e: "embed (Restr r A) r' f" and i: "B ⊆ A"<br>
shows "embed (Restr r B) r' f"<br>
using Well_order_Restr[OF r] ofilter_Restr_subset[OF r oB i] ofilter_embed2[OF _ e] Restr_subset[OF i] <br>
by fastforce</p>
<p>lemma ofilter_int: <br>
assumes r: "Well_order r" and oA: "ofilter r A" and oB: "ofilter r B"<br>
shows "ofilter r (A ∩ B)"<br>
by (metis Int_absorb1 Int_absorb2 oA oB r wo_rel.intro wo_rel.ofilter_linord)</p>
<p>lemma ofilter_UNION: <br>
assumes "∀ i ∈ I. ofilter r (A i)"<br>
shows "ofilter r (⋃ i ∈ I. A i)"<br>
using assms unfolding ofilter_def under_def by blast</p>
<p>lemma Restr_UNION_ofilter: <br>
assumes "∀ i ∈ I. ofilter r (A i)"<br>
shows "Restr r (⋃ i ∈ I. A i) = (⋃ i ∈ I. Restr r (A i))"<br>
using assms unfolding ofilter_def under_def by blast</p>
<p>lemma ofilter_ordLeq_UNION:<br>
assumes r: "Well_order r" and r': "Well_order r'"<br>
and ofl: "∀ i ∈ I. ofilter r (A i) ∧ ordLeq3 (Restr r (A i)) r'"<br>
shows "ordLeq3 (Restr r (⋃ i ∈ I. A i)) r'"<br>
proof-<br>
  let ?U = "⋃ i ∈ I. A i"<br>
  {fix i assume i: "i ∈ I"<br>
   have "Well_order (Restr r (A i))" using Well_order_Restr r by fastforce<br>
   have "∃ gi. embed (Restr r (A i)) r' gi" using ofl i unfolding ordLeq_def by auto<br>
  }<br>
  then obtain g where g: "∀ i ∈ I. embed (Restr r (A i)) r' (g i)" by metis<br>
  def ii ≡ "λ a. SOME i. i ∈ I ∧ a ∈ A i"<br>
  def gg ≡ "λ a. g (ii a) a"<br>
  have "ofilter r ?U" using ofilter_UNION ofl by metis<br>
  hence F: "Field (Restr r ?U) = ?U" using Field_Restr_ofilter[OF r] by auto<br>
  have "embed (Restr r ?U) r' gg" unfolding embed_def proof<br>
    fix a let ?i = "ii a"<br>
    assume "a ∈ Field (Restr r ?U)"<br>
    hence "∃ i. i ∈ I ∧ a ∈ A i" unfolding F by auto<br>
    hence i: "?i ∈ I" and aa: "a ∈ A ?i" unfolding ii_def by (metis (no_types, lifting) someI_ex)+<br>
    hence a: "a∈Field (Restr r (A ?i))" using Field_Restr_ofilter[OF r] ofl by auto<br>
    hence 1: "bij_betw (g ?i) (under (Restr r (A ?i)) a) (under r' (g ?i a))" using g i unfolding embed_def by auto<br>
    have 2: "under (Restr r ?U) a = under (Restr r (A ?i)) a"<br>
    using aa ofl i aa unfolding under_def ofilter_def by auto<br>
    {fix b assume 0: "b ∈ under (Restr r (A ?i)) a"<br>
     let ?j = "ii b"<br>
     have "b ∈ Field (Restr r ?U)" using 2 subsetCE under_Field 0 by fastforce<br>
     hence "∃ i. i ∈ I ∧ b ∈ A i" unfolding F by auto<br>
     hence j: "?j ∈ I" and bb: "b ∈ A ?j" unfolding ii_def by (metis (no_types, lifting) someI_ex)+<br>
     hence b: "b ∈ Field (Restr r (A ?j))" using Field_Restr_ofilter[OF r] ofl by auto<br>
     hence 1: "embed (Restr r (A ?j)) r' (g ?j)" using g j by auto<br>
     let ?B = "A ?i ∩ A ?j"<br>
     have b: "b ∈ ?B" by (metis 0 Int_iff bb mem_Collect_eq mem_Sigma_iff under_def)<br>
     have 11: "embed (Restr r (A ?i)) r' (g ?i)" using g i by auto<br>
     have oB: "ofilter r ?B" using ofl i j ofilter_int r by fastforce<br>
     hence B1: "embed (Restr r ?B) r' (g ?i)" using 11 i inf_le1 ofilter_incl_embed ofl r by fastforce  <br>
     have B2: "embed (Restr r ?B) r' (g ?j)" using 1 j by (metis oB inf_commute inf_le1 ofilter_incl_embed ofl r)<br>
     have B: "Well_order (Restr r ?B)" using Well_order_Restr r by blast<br>
     have "g ?i b = g ?j b" using b B1 B2 embed_unique[OF B r' B1 B2] Field_Restr_ofilter i j ofilter_int ofl r by blast<br>
    }<br>
    thus "bij_betw gg (under (Restr r ?U) a) (under r' (gg a))" using 1 2 unfolding gg_def <br>
    unfolding bij_betw_def inj_on_def image_def by auto<br>
  qed<br>
  thus ?thesis unfolding ordLeq_def using Restr_UNION_ofilter ofl r' <br>
  by (auto simp: Well_order_Restr r)<br>
qed</p>
<p>(* Cardinal suprema (for a family of cardinals on the same host type) *)</p>
<p>context fixes I :: "'i set" and k :: "'i ⇒ 'a rel" <br>
begin</p>
<p>definition K::"'a rel" where "K = card_of UNIV"</p>
<p>lemma card_order_K: "card_order K"<br>
by (simp add: Embed.K_def card_of_card_order_on)</p>
<p>lemma ordLeq_K: <br>
assumes "Card_order (k i)" shows "ordLeq3 (k i) K"<br>
by (metis Card_order_iff_ordLeq_card_of Embed.K_def UNIV_I assms card_of_mono1 <br>
          ordLeq_transitive subsetI)</p>
<p>definition f :: "'i ⇒ 'a ⇒ 'a" where <br>
"f i ≡ SOME f. embed (k i) K f"</p>
<p>lemma embed_f: <br>
assumes "Card_order (k i)"<br>
shows "embed (k i) K (f i)"<br>
unfolding f_def<br>
apply(rule someI_ex[of "%f. embed (k i) K f"])<br>
unfolding K_def <br>
by (metis Cnotzero_UNIV K_def assms card_order_on_def not_ordLess_ordLeq ordLeq_K ordLess_iff)</p>
<p>definition A :: "'i ⇒ 'a set" where <br>
"A i ≡ f i ` (Field (k i))"</p>
<p>lemma iso_A: <br>
assumes "Card_order (k i)"<br>
shows "iso (k i) (Restr K (A i)) (f i)"<br>
by (metis A_def Embed.K_def Embed.card_order_K Embed.embed_f Field_card_of <br>
          assms card_order_on_def embed_implies_iso_Restr)</p>
<p>lemma ordIso2_A: <br>
assumes "Card_order (k i)"<br>
shows "ordIso2 (k i) (Restr K (A i))"<br>
using iso_A[OF assms] assms card_order_on_def unfolding ordIso_def <br>
by (auto simp: K_def Well_order_Restr card_of_Well_order)</p>
<p>lemma Card_order_A: <br>
assumes "Card_order (k i)"<br>
shows "Card_order (Restr K (A i))"<br>
using Card_order_ordIso2 ordIso2_A[OF assms] assms by blast</p>
<p>definition csupr :: "'a rel" where <br>
"csupr ≡ card_of (⋃ i ∈ I. A i)"   </p>
<p>lemma Card_order_csupr: <br>
"Card_order csupr"<br>
by (simp add: csupr_def card_of_Card_order)</p>
<p>lemma ordLeq_csupr:<br>
assumes c:  "∀ i ∈ I. Card_order (k i)"  and i: "i ∈ I" <br>
shows "ordLeq3 (k i) csupr"<br>
proof-<br>
  let ?k = "card_of (Field (k i))"<br>
  have "ordIso2 (k i) (Restr K (A i))" using ordIso2_A c i by auto<br>
  also have "ordIso2 (Restr K (A i)) (card_of (A i))" <br>
  by (metis Card_order_A Embed.A_def c card_of_unique i iso_A iso_Field)<br>
  also have "ordLeq3 (card_of (A i)) csupr" unfolding csupr_def <br>
  by (meson UN_I assms(2) card_of_mono1 subsetI)<br>
  finally show ?thesis .<br>
qed</p>
<p>lemma ofilter_A: <br>
assumes ci: "Card_order (k i)" <br>
shows "ofilter K (A i)"<br>
using ci iso_A[OF ci] unfolding A_def<br>
by (metis Cnotzero_UNIV embed_f K_def card_order_on_well_order_on embed_Field_ofilter)</p>
<p>lemma csupr_ordLeq:<br>
assumes ci: "∀ i ∈ I. Card_order (k i)" <br>
and c: "Card_order r"<br>
and ord: "⋀ i. i ∈ I ⟹ ordLeq3 (k i) r" <br>
shows "ordLeq3 csupr r"<br>
proof-<br>
  let ?U = "⋃ i ∈ I. A i"<br>
  let ?restr = "Restr K ?U"<br>
  have 0: "∀i ∈ I. ofilter K (A i)" using ofilter_A ci by auto<br>
  have restr: "Well_order ?restr" and K: "Well_order K" and r: "Well_order r"<br>
  using c card_order_on_def by (auto simp: Field_card_of K_def Well_order_Restr card_of_well_order_on)<br>
  have "Field ?restr = ?U" <br>
  using ofilter_A ofilter_UNION 0 by (intro Field_Restr_ofilter) (auto simp: K_def Field_card_of card_of_well_order_on)<br>
  hence 1: "well_order_on ?U ?restr" using restr by auto<br>
  have restr: "?restr = (⋃ i ∈ I. Restr K (A i))" using Restr_UNION_ofilter[OF 0] by auto<br>
  have 2: "⋀ i. i ∈ I ⟹ ordLeq3 (Restr K (A i)) r" <br>
  using ord by (metis ordIso2_A ci ordIso_iff_ordLeq ordLeq_transitive)<br>
  have "ordLeq3 (card_of ?U) ?restr" using 1 card_of_least by blast<br>
  also have "ordLeq3 ?restr r" using ofilter_ordLeq_UNION[OF K r] 0 2 by metis<br>
  finally show ?thesis unfolding csupr_def .<br>
qed</p>
<p>end (* context *)</p>
<p>hide_const A K f</p>
<p>term csupr<br>
thm Card_order_csupr<br>
thm ordLeq_csupr<br>
thm csupr_ordLeq</p>
<p>definition "embeds A B ≡ ∃ f. f ` B ⊆ A ∧ inj_on f B"</p>
<p>lemma embeds_ordLeq: "embeds A B ⟷ ordLeq3 (card_of B) (card_of A)"<br>
unfolding card_of_ordLeq[symmetric] embeds_def by auto</p>
<p>lemma embeds_universal: <br>
fixes I :: "'i set" and F :: "'i ⇒ 'a set" <br>
defines "S ≡ Field (csupr I (card_of o F))"<br>
shows "(∀ i. i ∈ I ⟶ embeds S (F i)) ∧<br>
       (∀ S'. (∀ i. i ∈ I ⟶ embeds S' (F i)) ⟶ embeds S' S)"<br>
proof-  <br>
  have 1: "∀ i ∈ I. Card_order ((card_of o F) i)"<br>
  by (simp add: Field_card_of card_of_card_order_on)<br>
  have "ordIso2 (card_of S) (csupr I (card_of o F))" <br>
  unfolding S_def using card_of_Field_ordIso[OF Card_order_csupr] .<br>
  with ordLeq_csupr[OF 1] csupr_ordLeq[OF 1]<br>
  show ?thesis unfolding S_def embeds_ordLeq o_def <br>
  by safe (simp add: Field_card_of csupr_def, <br>
           simp add: card_of_Card_order csupr_ordLeq ordIso_ordLeq_trans)<br>
qed</p>
<p>end<br>
<a href="/user_uploads/14278/HqLeVj2OdcFbnB2M4VZz_qSY/Embed.thy">Embed.thy</a></p>



<hr><p>Last updated: Feb 22 2026 at 20:30 UTC</p>
</html>